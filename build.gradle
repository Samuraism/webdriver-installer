plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'one.cafebabe'
version '1.6'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.intellij:annotations:12.0'

    implementation 'org.apache.commons:commons-compress:1.21'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'webdriver-installer'
            from components.java
            pom {
                name = 'webdriver-installer'
                description = 'webdriver-installer'
                url = 'https://github.com/yusuke/webdriver-installer'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yusuke'
                        name = 'Yusuke Yamamoto'
                        email = 'yusuke@mac.com'
                    }
                }
                scm {
                    url = 'https://github.com/Samuraism/webdriver-installer'
                    connection = 'scm:git:git://github.com/Samuraism/webdriver-installer.git'
                    developerConnection = 'scm:git:git@github.com:Samuraism/webdriver-installer.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['SONATYPE_USERNAME'] ?: "none"
                password = project.properties['SONATYPE_PASSWORD'] ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
